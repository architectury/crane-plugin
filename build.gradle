plugins {
    id "java"
    id "java-gradle-plugin"
    id "org.cadixdev.licenser" version "0.6.1" apply false
}

repositories {
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.architectury.dev/" }
    gradlePluginPortal()
}

allprojects {
    apply plugin: "org.cadixdev.licenser"
    apply plugin: "maven-publish"

    def runNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "9999"
    group "dev.architectury"
    version "0.2.$runNumber"

    license {
        header = rootProject.file("HEADER")

        ext {
            name = "architectury"
            year = "2021"
        }

        include "**/*.java"
    }

    publishing {
        repositories {
            if (System.getenv("MAVEN_PASS") != null) {
                maven {
                    url = "https://deploy.shedaniel.me/"
                    credentials {
                        username = "shedaniel"
                        password = System.getenv("MAVEN_PASS")
                    }
                }
            }
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation "cuchaz:enigma:${project.enigma_version}"
    implementation "cuchaz:enigma-cli:${project.enigma_version}"
    implementation "cuchaz:enigma-swing:${project.enigma_version}"
    implementation "net.fabricmc:stitch:${project.stitch_version}"
    implementation "com.google.guava:guava:${project.guava_version}"
    implementation "commons-io:commons-io:${project.apache_commons_versions}"
    implementation "com.google.code.gson:gson:${project.gson_versions}"
    implementation "dev.architectury:tiny-remapper:${project.tiny_remapper_versions}"
    implementation "me.tongfei:progressbar:0.9.0"
    implementation("net.fabricmc:lorenz-tiny:3.0.0") {
        transitive = false
    }
    implementation "org.cadixdev:lorenz-io-proguard:0.5.6"
    implementation "org.cadixdev:lorenz:0.5.3"
    implementation "dev.architectury:mappings-layers-core:1.2.7"
    implementation "net.fabricmc:tiny-mappings-parser:0.3.0+build.17"
    implementation "dev.architectury:architectury-transformer:$transformer_version"
    def asm_version = "9.3"
    implementation "org.ow2.asm:asm:$asm_version"
    implementation "org.ow2.asm:asm-analysis:$asm_version"
    implementation "org.ow2.asm:asm-commons:$asm_version"
    implementation "org.ow2.asm:asm-tree:$asm_version"
    implementation "org.ow2.asm:asm-util:$asm_version"
}

gradlePlugin {
    plugins {
        plugin {
            id = "dev.architectury.crane-plugin"
            implementationClass = "dev.architectury.plugin.crane.CranePlugin"
        }
    }
}
